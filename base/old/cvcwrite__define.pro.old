;
;  NAME: 
;    cvcWrite
;
;  PURPOSE:
;    High-level base class. Provides writing of results.
;
;  INHERITS:
;    cvcError
;
;  CALLING SEQUENCE:
;    None 
;
;--------------------------------------------------------------------
FUNCTION cvcWrite::Init, in_oGlobal

    self.oGlobal = in_oGlobal
    hLogFile = self.oGlobal->Get('hLogFile')
    res = self->cvcError::Init(hLogFile)
    self.oConsts = obj_new('cvcConstants', in_oGlobal)
    self.hLogFile = hLogFile

    return, 1
END
;--------------------------------------------------------------------
PRO cvcWrite::Cleanup
    if (obj_valid(self.oConsts)) then obj_destroy, self.oConsts
END
;--------------------------------------------------------------------
FUNCTION cvcWrite::__WriteNetCDF, in_sData, in_sDestDesc
    
    self->printLog, '(cvcWrite) Writing netCDF...'
    
    nData = n_elements(in_sData)
    dataKind = (*in_sData[0].data).kind
    
; check if all datasets are of the same kind
    for i = 1, nData-1 do begin
      if ((*in_sData[i].data).kind ne dataKind) then begin
        self->printLog, 'Error: All data MUST be of the same kind to be written into one file!'
        return, -1
      endif
    endfor
  
; open file
    self->printLog, '(cvcWrite) Open file'
    id = ncdf_create(in_sDestDesc.file.name, /clobber)
    
; for data fields
    if (dataKind eq 'map') then begin
      self->printLog, '(cvcWrite) Data kind is < map >' ;. Checking datasets...', format='(a, $)'
      dataSize = size(*(*(in_sData[0]).data).data)
; This checking was removed to increase performance
;      for i = 1, nData-1 do begin
;        ; check if all datasets are of the same size
;        if ((n_elements(*(*(in_sData[i]).data).data) ne n_elements(*(*(in_sData[0]).data).data)) or $
;            (n_elements(*(*(in_sData[0]).data).pXgrid) ne n_elements(*(*(in_sData[i]).data).pXgrid)) or $
;            (n_elements(*(*(in_sData[0]).data).pYgrid) ne n_elements(*(*(in_sData[i]).data).pYgrid))) then begin
;          self->printLog, 'Error: All data MUST be of the same size and have the same lon-lat grid to be written into one file!'
;          return, -1
;        endif
;      endfor
;      self->printLog, 'OK', /notimestamp
      
      self->printLog, '(cvcWrite) Define lon/lat dimensions and dimension variables'
; longitude variable
      if (ptr_valid((*in_sData[0].data).pXgrid) and ptr_valid((*in_sData[0].data).pYgrid)) then begin
        lonGridSize = size(*(*in_sData[0].data).pXgrid)
        latGridSize = size(*(*in_sData[0].data).pYgrid)
        if (lonGridSize[0] ne latGridSize[0]) then begin
          self->printLog, '(cvcWrite::__WriteNetCDF) Error: XGrid and YGrid dimensions are not equal!'
          return, -1
        endif
        gridSize = lonGridSize[0]

        ; 1-D grid variables
        if (gridSize eq 1) then begin
          self->printLog, '(cvcWrite) 1-D grids are used'
          ; lon-lat dimensions
          if (n_elements(*(*(in_sData[0]).data).pXgrid) eq dataSize[1]) then begin
            xid = ncdf_dimdef(id, 'lon', dataSize[1])
          endif else begin
            self->printLog, '(cvcWrite::__WriteNetCDF) Error: X grid does not correspond data!'
            return, -1
	  endelse
	  
          if (n_elements(*(*(in_sData[0]).data).pYgrid) eq dataSize[2]) then begin
            yid = ncdf_dimdef(id, 'lat', dataSize[2])
          endif else begin
            self->printLog, '(cvcWrite::__WriteNetCDF) Error: Y grid does not correspond data!'
            return, -1
	  endelse

          lonid = ncdf_vardef(id, 'lon', [xid], /float)
          latid = ncdf_vardef(id, 'lat', [yid], /float)
        endif
        
        ; 2-D grid variables
        if (gridSize eq 2) then begin
          self->printLog, '(cvcWrite) 2-D grids are used'
          xid = ncdf_dimdef(id, 'xc', dataSize[1])
          yid = ncdf_dimdef(id, 'yc', dataSize[2])
          xcvarid = ncdf_vardef(id, 'xc', [xid], /float)
          ycvarid = ncdf_vardef(id, 'yc', [yid], /float)
          
          lonid = ncdf_vardef(id, 'lon', [xid, yid], /float)
          latid = ncdf_vardef(id, 'lat', [xid, yid], /float)
        endif

        ; bad grid variables
        if (gridSize gt 2) then begin
          self->printLog, '(cvcWrite::__WriteNetCDF) Error: XGrid and YGrid have more than 2 dimensions'
          return, -1
        endif
        
        ncdf_attput, id, lonid, 'units', 'degrees_east'
        ncdf_attput, id, lonid, 'long_name', 'Longitude'
        ncdf_attput, id, latid, 'units', 'degrees_north'
        ncdf_attput, id, latid, 'long_name', 'Latitude'
        aDimIDs = [xid, yid]
      endif

; level dimension
      if (ptr_valid((*in_sData[0].data).pZgrid)) then begin
        self->printLog, '(cvcWrite) Define level dimension and variable'
        zid = ncdf_dimdef(id, 'lev', n_elements(*(*in_sData[0].data).pZgrid))
        levid = ncdf_vardef(id, 'lev', [zid], /float)
        ncdf_attput, id, levid, 'units', '-'
        ncdf_attput, id, levid, 'long_name', 'Level'
        if (n_elements(*(*in_sData[0].data).pZgrid) gt 1) then aDimIDs = [aDimIDs, zid] 
      endif
; time dimension
      if (ptr_valid((*in_sData[0].data).pTimeGrid)) then begin
        self->printLog, '(cvcWrite) Define time dimension and variable'
        tid = ncdf_dimdef(id, 'time', n_elements(*(*in_sData[0].data).pTimeGrid))
        timeid = ncdf_vardef(id, 'time', [tid], /double)
        ncdf_attput, id, timeid, 'units', 'hours since 1979-1-1 00:00:0.0'
        ncdf_attput, id, timeid, 'long_name', 'Time'
        if (n_elements(*(*in_sData[0].data).pTimeGrid) gt 1) then aDimIDs = [aDimIDs, tid] 
      endif
; global attributes
      self->printLog, '(cvcWrite) Write global attributes'
      ncdf_attput, id, /global, 'Title', (*in_sData[0].data).description.title
      ncdf_attput, id, /global, 'Conventions', 'CF'
      ncdf_attput, id, /global, 'Source', 'Generated by CLEARS system'
      
; define data variables and attributes
      self->printLog, '(cvcWrite) Define data variables and attributes'
      dataId = intarr(nData)
      for iData = 0, nData-1 do begin
        dataId[iData] = ncdf_vardef(id, 'data'+strtrim(string(iData), 2), aDimIDs, /float)
        ncdf_attput, id, dataId[iData], 'units', (*in_sData[iData].data).description.units
        ncdf_attput, id, dataId[iData], 'long_name', (*in_sData[iData].data).description.name
        ncdf_attput, id, dataId[iData], 'missing_value', (*in_sData[iData].data).missingVal
        ncdf_attput, id, dataId[iData], 'coordinates', 'lon lat'
      endfor
      
; put file in data mode
      self->printLog, '(cvcWrite) Put file in data mode'
      ncdf_control, id, /endef

; put variables
      self->printLog, '(cvcWrite) Write grids'
      ncdf_varput, id, lonid, *(*(in_sData[0]).data).pXgrid
      ncdf_varput, id, latid, *(*(in_sData[0]).data).pYgrid

      if (ptr_valid((*in_sData[0].data).pZgrid)) then begin
        for i = 0, n_elements(*(*(in_sData[0]).data).pZgrid)-1 do begin
          if (size((*(*(in_sData[0]).data).pZgrid)[i], /type) eq 7) then begin ; string type
             case (*(*(in_sData[0]).data).pZgrid)[i] of
                'sfc': curlev = 0
                '2m': curlev = 1
                '10m' : curlev = 2
                else: begin
                   self->printLog, 'Unrecognized level string value: '+(*(*(in_sData[0]).data).pZgrid)[i]+'. Trying to convert it to integer. Otherwise defalut = 0 will be used'
                   curlev = fix((*(*(in_sData[0]).data).pZgrid)[i])
                end
             endcase
          endif else curlev = (*(*(in_sData[0]).data).pZgrid)[i]
          if (i eq 0) then levs = curlev else levs = [levs, curlev]
       endfor
        ncdf_varput, id, levid, levs
      endif

      if (ptr_valid((*in_sData[0].data).pTimeGrid)) then begin
        ncdf_varput, id, timeid, (*(*(in_sData[0]).data).pTimeGrid - julday(1, 1, 1979, 0, 0))*24.0
      endif
; put data
      self->printLog, '(cvcWrite) Write data...', format='(a, $)'
      for iData = 0, nData-1 do begin
        ncdf_varput, id, dataId[iData], *(*(in_sData[iData]).data).data
      endfor
      self->printLog, 'OK', /notimestamp
    endif

; for stations
    if (dataKind eq 'stations') then begin ; we have a set of stations
      self->printLog, '(cvcWrite) Data kind is < stations >'
      self->printLog, '(cvcWrite) Define dimensions and attributes'
      ; define dimensions
      stid = ncdf_dimdef(id, 'station', /unlimited)
      ; define variables
      lonid = ncdf_vardef(id, 'longitude', [stid], /float)
      latid = ncdf_vardef(id, 'latitude', [stid], /float)
      dataid = ncdf_vardef(id, 'data', [stid], /float)
      ; define attributes
      ncdf_attput, id, lonid, 'units', 'degrees_east'
      ncdf_attput, id, lonid, 'long_name', 'station longitude'
      ncdf_attput, id, latid, 'units', 'degrees_north'
      ncdf_attput, id, latid, 'long_name', 'station latitude'
      ncdf_attput, id, dataid, 'units', (*in_sData[0].data).description.units
      ncdf_attput, id, dataid, 'long_name', (*in_sData[0].data).description.name
      ncdf_attput, id, dataid, 'coordinates', 'lat lon'    
      ncdf_attput, id, /global, 'Title', (*in_sData[0].data).description.title
      ; put file in data mode
      self->printLog, '(cvcWrite) Put file in data mode'
      ncdf_control, id, /endef
      ; concatenate data
      xGrid = *(*(in_sData[0]).data).pXgrid
      yGrid = *(*(in_sData[0]).data).pYgrid
      data = *(*(in_sData[0]).data).data
      for iData = 1, nData-1 do begin
        xGrid = [xGrid, *(*(in_sData[iData]).data).pXgrid]
        yGrid = [yGrid, *(*(in_sData[iData]).data).pYgrid]
        data = [data, *(*(in_sData[iData]).data).data]
      endfor
      ; put data
      self->printLog, '(cvcWrite) Write data...', format='(a, $)'
      ncdf_varput, id, lonid, xGrid
      ncdf_varput, id, latid, yGrid
      ncdf_varput, id, dataid, data
      self->printLog, 'OK', /notimestamp
    endif
  
    if (dataKind eq 'lines') then begin ; we have a set of lines
      self->printLog, '(cvcWrite) Data kind is < lines >'
      ; calculate number of lines and number of points
      nLines = 0
      nPoints = 0
      for iLine = 0, nData-1 do begin
        dataSize = size(*(*(in_sData[iLine]).data).pXgrid)
        if (dataSize[0] eq 1) then begin
          nLines = nLines + 1
          nPoints = max([nPoints, dataSize[1]])
        endif else $
        if (dataSize[0] eq 2) then begin
          nLines = nLines + dataSize[1]
          nPoints = max([nPoints, dataSize[2]])
        endif else begin
          self->printLog, '(cvcWrite::__WriteNetCDF) Error: Too much dimensions specfied to write simple lines'
          return, -1
        endelse
      endfor
      ; define dimensions
      lineid = ncdf_dimdef(id, 'line', nLines)
      pointid = ncdf_dimdef(id, 'point', nPoints)
      ; define variables
      xid = ncdf_vardef(id, 'x', [pointid, lineid], /float)
      yid = ncdf_vardef(id, 'y', [pointid, lineid], /float)
        ; define attributes
      ; put file in data mode
      ncdf_control, id, /endef
      ; put data
      self->printLog, '(cvcWrite) Write data...', format='(a, $)'
      iPos = 0
      for iLine = 0, nData-1 do begin
        dataSize = size(*(*(in_sData[iLine]).data).pXgrid)
        if (dataSize[0] eq 1) then begin
          ncdf_varput, id, xid, *(*(in_sData[iLine]).data).pXgrid, offset=[0, iPos]
          ncdf_varput, id, yid, *(*(in_sData[iLine]).data).pYgrid, offset=[0, iPos]
          iPos = iPos + 1        
        endif else $
        if (dataSize[0] eq 2) then begin
          for iSet = 0, dataSize[1]-1 do begin
            ncdf_varput, id, xid, ((*(*(in_sData[iLine]).data).pXgrid)[iSet, *])[*], offset=[0, iPos]
            ncdf_varput, id, yid, ((*(*(in_sData[iLine]).data).pYgrid)[iSet, *])[*], offset=[0, iPos]
            iPos = iPos + 1
          endfor
        endif
      endfor
      self->printLog, 'OK', /notimestamp
    endif
    
; close file

    self->printLog, '(cvcWrite) Closing file...', format='(a, $)'
    ncdf_close, id
    self->printLog, 'OK', /notimestamp

    return, self.ERROR_OK
END
;--------------------------------------------------------------------
FUNCTION cvcWrite::__WriteGeoTIFF, in_sData, in_sDestDesc

    dataKind = (*in_sData[0].data).kind
    if ((dataKind ne 'map') and (~ptr_valid((*in_sData.data).extra))) then begin
      self->printLog, 'Error: GeoTIFF is only for field data!'
      return, -1
    endif
    
    if (~ptr_valid((*in_sData[0].data).extra)) then begin
      minLon = min(*(*in_sData[0].data).pXgrid, max = maxLon)
      minLat = min(*(*in_sData[0].data).pYgrid, max = maxLat)
      sizes = size(*(*in_sData[0].data).data)
      imageXsize = sizes[1]
      imageYsize = sizes[2]
  
      xScale = (maxLon - minLon) / (imageXsize - 1)
      yScale = (maxLat - minLat) / (imageYsize - 1)
  
  ; prepare geokeys    
      ModelTypeGeographic  = 2   ;/* Geographic latitude-longitude System */
      RasterPixelIsArea  = 1
      GCS_WGS_84 =  4326
      
      xGrid = (*(*(in_sData).data).pXgrid)
      if (maxLon gt 180) then xGrid = (xGrid + 180.) mod 360. -180.
      
      dataSize = size(*(*(in_sData[0]).data).data)
      modelTPT = dblarr(6*dataSize[1]*dataSize[2])
      
      for i = 0, dataSize[1]-1 do begin
        for j = dataSize[2]-1, 0, -1 do begin
          idx = (i*dataSize[2] + j)*6
          modelTPT[idx+0] = double(i)
          modelTPT[idx+1] = double(j)
          modelTPT[idx+2] = 0.0D
          modelTPT[idx+3] = xGrid[i]
          modelTPT[idx+4] = (*(*(in_sData).data).pYgrid)[j]
          modelTPT[idx+5] = 0.0D
        endfor
      endfor
  
      geokeys = { MODELPIXELSCALETAG : [ xScale, yScale, 0.0D ], $
                  MODELTIEPOINTTAG : modelTPT, $
                  GTMODELTYPEGEOKEY : ModelTypeGeographic, $
                  GTRASTERTYPEGEOKEY : RasterPixelIsArea, $
                  GEOGRAPHICTYPEGEOKEY : GCS_WGS_84 $
                }
    endif else geokeys = *(*in_sData.data).extra
; write geotiff    
    write_tiff, in_sDestDesc.file.name, *(*in_sData.data).data, geotiff = geokeys, /float
    
    return, self.ERROR_OK
END
;--------------------------------------------------------------------
FUNCTION cvcWrite::__WriteBin, in_sData, in_sDestDesc

    return, self.ERROR_OK
END
;--------------------------------------------------------------------
FUNCTION cvcWrite::__WriteXML, in_sData, in_sDestDesc

    return, self.ERROR_OK
END
;--------------------------------------------------------------------
FUNCTION cvcWrite::__CheckGrid, in_sData 
    
    if (ptr_valid(in_sData.pData)) then dataSize = size(*in_sData.pData)
    if (ptr_valid(in_sData.pXgrid)) then xSize = size(*in_sData.pXgrid)
    if (ptr_valid(in_sData.pYgrid)) then ySize = size(*in_sData.pYgrid)
    if (ptr_valid(in_sData.pZgrid)) then zSize = size(*in_sData.pZgrid)
    if (ptr_valid(in_sData.pTimegrid)) then timeSize = size(*in_sData.pTimeGrid)
    gridType = in_sData.gridType
    
    return, self.ERROR_OK
END
;--------------------------------------------------------------------
FUNCTION cvcWrite::Run, in_pInputs


    ; destination description
    idx = where((*in_pInputs).type eq 'destination')
    if (idx[0] eq -1) then begin
      self->printLog, 'Error: No destination!'
      return, -1
    endif
    sDestDesc = *((*in_pInputs)[idx[0]].data)
    
    ; data to write
    idx = where((*in_pInputs).type eq 'data')
    if (idx[0] eq -1) then begin
      self->printLog, 'Error: Nothing to write!'
      return, -1
    endif
    sData = (*in_pInputs)[idx]
        
    if (sDestDesc.file.name ne '') then begin
      outFileType = sDestDesc.file.type 
    endif else begin
      self->printLog, 'Error: No file name specified!'
      return, -1
    endelse
    
    case outFileType of
      'netcdf': ret = self->__WriteNetCDF(sData, sDestDesc)
      'shape': ret = self->__WriteShp(sData, sDestDesc)
      'geotiff': ret = self->__WriteGeoTIFF(sData, sDestDesc)
      'bin': ret = self->__WriteBin(sData, sDestDesc)
      'xml': ret = self->__WriteXML(sData, sDestDesc)
      else: begin
        self->printLog, 'Error! Bad file type!'
        return, -1
      end
    endcase
    if (self->Assert(ret)) then return, ret

    return, self.ERROR_OK
END
;--------------------------------------------------------------------
PRO cvcWrite__define

    MAX_N_RESULTS = 128

    struct = { cvcWrite, $
		  oGlobal : obj_new(), $ ; global data
                  oConsts : OBJ_NEW(), $
                  INHERITS cvcError $
             }
END
